/**
 * Copyright (C) 2017 - 2020 TQ-Systems GmbH
 * @author Markus Niebel <Markus.Niebel@tq-group.com>
 * Jenkinsfile for TQ-Systems Yocto-BSPs using declarative pipeline
 * License:     GPLv2
 *
 */

import java.text.*
import groovy.time.*

/**********************************************************************/
/*******************         Functions         ************************/
/**********************************************************************/
def wipeOutWs(){
    dir("${env.WORKSPACE}") {
        echo "Remove ${env.WORKSPACE} ..."
        deleteDir()
    }
    dir("${env.WORKSPACE}@script") {
        echo "Remove ${env.WORKSPACE}@script ..."
        deleteDir()
    }
    dir("${env.WORKSPACE}@tmp") {
        echo "Remove ${env.WORKSPACE}@tmp ..."
        deleteDir()
    }
}

def checkoutMetaLayers(directoryName) {
    dir("${directoryName}") {
        sshagent(['jenkins-master-key']) {
            sh """
            git submodule sync
            git submodule foreach --recursive git clean -fdx
            git submodule foreach --recursive git checkout .
            git submodule update --init
            git submodule foreach --recursive git clean -fdx
            git submodule foreach --recursive git checkout .
            git clean -fdx sources templates
            git clean -fx"""
        }
    }
}

def prepareWorkspace(directoryName){
    if (params.CLEAN_CACHES == true) {
        dir("${directoryName}/${env.BUILD_DIR}") {
            echo "Remove ${directoryName}/${env.BUILD_DIR} ... (CLEAN_CACHES)"
            deleteDir()
        }
    }
    checkout([
        $class: 'GitSCM',
        branches: [[name: "${YOCTO_VERSION}"]],
        doGenerateSubmoduleConfigurations: false,
        extensions: [
            [$class: 'SubmoduleOption',
                disableSubmodules: true,
                parentCredentials: true,
                recursiveSubmodules: true,
                reference: '',
                trackingSubmodules: false
            ],
            [$class: 'RelativeTargetDirectory',
                relativeTargetDir: "${directoryName}"
            ],
            [$class: 'PruneStaleBranch'],
        ],
        submoduleCfg: [],
        userRemoteConfigs: [[
            credentialsId: 'jenkins-master-key',
            url: "${env.GIT_URL}"
        ]]
    ])
    echo "create shared DL_DIR ${env.WORKSPACE}/${env.DL_DIR} ..."
    sh "mkdir -p ${env.WORKSPACE}/${env.DL_DIR}"
}

def mirrorConfiguration(directoryName) {
    dir("${directoryName}") {
        sshagent(['jenkins-master-key']) {
            echo "creat mirror packages for all machines of config ${directoryName} ..."
            sh "./ci/build-all ${env.BUILD_DIR}-mirror \$(basename \$(pwd)) mirror"
            sh "cp -ruv ${env.BUILD_DIR}-mirror/downloads/* ${env.WORKSPACE}/${env.DL_DIR}"
        }
    }
}

def buildConfiguration(directoryName) {
    dir("${directoryName}") {
        echo "build all machines for config ${directoryName} ..."
        sh "export SSTATE_DIR=${env.WORKSPACE}/${env.SSTATE_DIR} && export DL_DIR=${env.WORKSPACE}/${env.DL_DIR} && ./ci/build-all ${env.BUILD_DIR} \$(basename \$(pwd)) ci"
    }
}

def wipeOutBuildspace(directoryName){
    dir("${directoryName}/${env.BUILD_DIR}/${env.DEPLOY_DIR}") {
        echo "Remove ${directoryName}/${env.BUILD_DIR}/${env.DEPLOY_DIR} ..."
        deleteDir()
    }
    dir("${directoryName}@script") {
        echo "Remove ${directoryName}@script ..."
        deleteDir()
    }
    dir("${directoryName}@tmp") {
        echo "Remove ${directoryName}@tmp ..."
        deleteDir()
    }
}

/*
 * delete complete build dir for manual triggered builds (RELEASE) and nightly
 * builds (timer triggered)
 * use shell rm in favour of dir/deleteDir
 *
 * since version 2.24 of workflow-support plugin currentBuild.getBuildCauses()
 * can be used. as an additional benefit - this method is whitelisted  - no need
 * for manual script security exclusions
 */
def handleBuildCause(directoryName) {
    def causes = currentBuild.getBuildCauses()
    println causes.toString()

    for(cause in causes) {
        def reason = cause.toString()
        cause = null
        if (reason.contains("TimerTriggerCause")) {
            println "This job was caused by Timer, rm tmp ... (NIGHTLY)"
            sh "rm -rf ${directoryName}/${env.BUILD_DIR}/tmp"
        } else if (reason.contains("UserIdCause")) {
            println "This job was caused by user, rm ${env.BUILD_DIR} ... (RELEASE)"
            sh "rm -rf ${directoryName}/${env.BUILD_DIR}"
        } else {
            println "build cause : " + reason
        }
    }
    causes = null
}

def handleGlobalData(directoryName) {
    def causes = currentBuild.getBuildCauses()

    for(cause in causes) {
        def reason = cause.toString()
        cause = null
        /*
         * for all build cases other then SCM triggered rebuild delete the
         * DL_DIR
         */
        if (!reason.contains("SCMTriggerCause")) {
            println "clear ${directoryName}/${env.DL_DIR} ..."
            sh "pwd; ls -al; time rm -rf ${directoryName}/${env.DL_DIR}; ls -1"
        }
        /*
         * for nightly builds: do sstate cache maintenance
         * to make sure that no previous error left defect build space,
         * update submodules before
         */
        if (reason.contains("TimerTriggerCause")) {
            println "maintain ${directoryName}/${env.SSTATE_DIR} ..."
            checkoutMetaLayers("${directoryName}")
            sh "time ${directoryName}/sources/poky/scripts/sstate-cache-management.sh --cache-dir=${directoryName}/${env.SSTATE_DIR} --extra-layer=${directoryName}/sources/meta-tq -d -y"
        }
    }
    causes = null

    /*
     * last ressort only: clean SSTATE_CACHE
     */
    if (params.CLEAN_CACHES == true) {
        println "clear ${directoryName}/${env.SSTATE_DIR} ... (CLEAN_CACHES)"
        sh "pwd; ls -al; time rm -rf ${directoryName}/${env.SSTATE_DIR}; ls -1"
    }

    /*
     * remove old archives
     */
    echo "clear ${directoryName}/*.tar.gz ... "
    sh "rm -f ${directoryName}/*.tar.gz"
}
/**********************************************************************/
/*******************         Pipeline         *************************/
/**********************************************************************/
/*
 * jenkins environment for this pipeline:
 * - GIT_URL: git URL for project to build (job config)
 * - MAIL_RECIPIENTS: comma separated list of mail recipients (jenkins global config)
 * - YOCTO_VERSION: git ref to build (job config)
 * - CLEAN_CACHES (job config)
 */
pipeline {
    agent {
        label 'master'
    }

    options {
        buildDiscarder(logRotator(artifactDaysToKeepStr: '', 
                       artifactNumToKeepStr: '3', 
                       daysToKeepStr: '', 
                       numToKeepStr: '10')
        )
        disableConcurrentBuilds()
        timestamps()
    }

    triggers {
        /* nightly rebuild */
        cron('H H(0-4) * * *')
        /*
         * TODO: we should use gitlab webhooks here ...
         * pollSCM('''# Alle f√ºnf Minuten von 7.00-20.00 an jedem Wochentag
         *       H/5 7-20 * * 1-5''')
         */
    }

    environment {
        /*
         * workspace name for config must be identical to config name
         */
        WS_TQ = "CONFIGS/tq"
        DL_DIR = "shared-dl-dir"
        SSTATE_DIR = "shared-sstate-cache"
        BUILD_DIR = "buildspace"
        DEPLOY_DIR = "deployment"
    }

    stages {
        stage ('Manage sstate cache') {
            steps {
                /*
                 * manageSstateCache(currentBuild.startTimeInMillis()) does not work
                 * this helps to explicit clean state cache
                 */
                handleGlobalData("${env.WORKSPACE}")
            }
        }
        stage ('Prepare Workspaces') {
            steps {
                parallel (
                    "tq" : {
                        wipeOutBuildspace("${env.WS_TQ}")
                        prepareWorkspace("${env.WS_TQ}")
                    },
                )
            }
        }
        stage('handle build cause') {
            steps {
                parallel (
                    "tq" : {
                        handleBuildCause("${env.WS_TQ}")
                    },
                )
            }
        }
        stage ('Checkout all meta-layers') {
            steps {
                parallel (
                    "tq" : {
                        checkoutMetaLayers("${env.WS_TQ}")
                    },
                )
            }
        }
        stage ('Fetch'){
            steps {
                parallel (
                    "tq" : {
                        mirrorConfiguration("${env.WS_TQ}")
                    },
                )
            }
        }
        stage ('Build'){
            steps {
                parallel (
                    "tq" : {
                        buildConfiguration("${env.WS_TQ}")
                    },
                )
            }
       }
       stage ('Archive'){
            steps {
                script {
                    checkoutMetaLayers("${env.WORKSPACE}")
                    sshagent(['jenkins-master-key']) {
                        sh "export STAMP=\$(${env.WORKSPACE}/ci/git-revision-name.sh \"${env.YOCTO_VERSION}\") && ${env.WORKSPACE}/ci/git-archive-all.sh --format tar.gz --prefix ci-meta-tq.\${STAMP}/ ci-meta-tq.\${STAMP}.tar"
                    }
                }
            }
        }
    }

    post {
        success {
            archiveArtifacts artifacts: "${env.WS_TQ}/${env.BUILD_DIR}/${env.DEPLOY_DIR}/*"
            wipeOutBuildspace("${env.WS_TQ}")
            archiveArtifacts artifacts: "ci-meta-tq.*.tar.gz"
        }
        unstable {
            emailext (
                to: "${env.MAIL_RECIPIENTS}",
                subject: "Build Notification: ${env.JOB_BASE_NAME} - Build # ${env.BUILD_NUMBER} - ${currentBuild.result}!",
                body: """ ${env.JOB_BASE_NAME} - Build # ${env.BUILD_NUMBER} - ${currentBuild.result}:
                  Check console output at ${env.BUILD_URL} to view the results.""",
                attachLog: true,
            )
        }
        failure {
            emailext (
                to: "${env.MAIL_RECIPIENTS}",
                subject: "Build Notification: ${env.JOB_BASE_NAME} - Build # ${env.BUILD_NUMBER} - ${currentBuild.result}!",
                body: """\
                ${env.JOB_BASE_NAME} - Build # ${env.BUILD_NUMBER} - ${currentBuild.result}:
                Check console output at ${env.BUILD_URL} to view the results.""",
                attachLog: true,
            )
        }
    }
}
